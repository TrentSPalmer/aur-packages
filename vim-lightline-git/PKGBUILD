# $Id$
# Maintainer: trent palmer <trenttdually at g mail>
# Contributor: jeff tracy <jefftracy nine one one at aol dot com> 
# Forked from https://aur.archlinux.org/vim-lightline-git.git
# at commit 95ac7c5e2434144e5a40e4b71432bf9d61203cac

###########################################################################################################
#                                          Build Options
###########################################################################################################
_vim="y"

# if you have a newer version of neovim you may not need or want to set _neovim="y"
_neovim="n"
###########################################################################################################

_name='vim-lightline'
pkgname="${_name}-git"
pkgver=r682.f11645c
pkgrel=1
pkgdesc='A light and configurable statusline/tabline for Vim'
arch=('any')
url='https://github.com/itchyny/lightline.vim'
license=('MIT')
depends=('vim-runtime' 'neovim')
makedepends=('git')
provides=('vim-lightline')
conflicts=('vim-lightline' 'vim-lightline-git')
replaces=('')
install='lightline.install'
source=("${_name}::git+${url}")
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_name}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

package() {
  if [ "$_neovim" = "y" ]; then
    mkdir -p "$pkgdir/usr/share/nvim/runtime"
    cp -r "${srcdir}/${_name}/"{autoload,doc,plugin} \
        "$pkgdir/usr/share/nvim/runtime"
  fi
    
  if [ "$_vim" = "y" ]; then
    mkdir -p "$pkgdir/usr/share/vim/vimfiles"
    cp -r "${srcdir}/${_name}/"{autoload,doc,plugin} \
        "$pkgdir/usr/share/vim/vimfiles"
  fi

    install -D -m644 "${srcdir}/${_name}/README.md" $pkgdir/usr/share/doc/$pkgname/README.md
    install -D -m644 "${srcdir}/${_name}/LICENSE" $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

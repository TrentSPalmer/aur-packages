# Maintainer: Trent
# Contributor: Matthias Lisin <ml@visu.li>
# Contributor: Hugo Rodrigues <hugorodrigues at openmailbox dot com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=vim-dart-plugin-git
pkgver=r87.8ffc3e2
pkgrel=1
pkgdesc='Syntax highlighting for Dart in Vim'
arch=('any')
url='https://github.com/dart-lang/dart-vim-plugin'
license=('BSD')
depends=('vim-runtime')
makedepends=('git')
conflicts=('vim-dart-plugin')
provides=('vim-dart-plugin')
source=(git+https://github.com/dart-lang/dart-vim-plugin.git)
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/dart-vim-plugin"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "dart-vim-plugin"
    local installpath="$pkgdir/usr/share/vim/vimfiles"
    install -dm755 "$installpath"
    cp -rt "$installpath" autoload doc ftdetect ftplugin indent plugin syntax
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
